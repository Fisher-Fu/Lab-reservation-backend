plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'org.graalvm.buildtools.native' version '0.9.23'
}

group = 'kreadcn.homework'
version = '2.0'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    mybatisGenerator
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    //implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.2'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'com.alibaba.fastjson2:fastjson2:2.0.31'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
    implementation 'org.apache.poi:poi:5.2.3'
    implementation 'org.apache.poi:poi-ooxml:5.2.3'
    implementation 'cn.wanghaomiao:JsoupXpath:2.5.3'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    mybatisGenerator files('lib/mybatis-generator-core-reform-1.3.5.jar')
    mybatisGenerator 'com.mysql:mysql-connector-j'
    mybatisGenerator 'org.projectlombok:lombok'
}

tasks.named('test') {
    useJUnitPlatform()
}
tasks.withType(JavaCompile) {
    options.encoding = "utf-8"
}

task mybatisGenerate {
    group 'mybatis generator'
    ant.taskdef(
            name: 'mbgenerator',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.mybatisGenerator.asPath
    )
    doLast {
        ant.mbgenerator(
                overwrite: true,
                configfile: 'src/main/resources/generatorConfig.xml', verbose: true
        )
    }
}
graalvmNative {
    binaries {
        main {
            buildArgs.add('--language:js')
            buildArgs.add('-H:ReflectionConfigurationFiles=../../../reflect-config.json')
            buildArgs.add('-H:+AddAllCharsets')
            buildArgs.add('-J-Dfile.encoding=UTF-8')
        }
    }
}

